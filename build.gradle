import java.time.ZonedDateTime

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = "net.time4tea"
version = project.hasProperty('VERSION') ? project.VERSION : 'LOCAL'

description = "A small library to help you control time in your application, so you can test it"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes 'Implementation-Title': 'tea-time'
        attributes 'Implementation-Version': project.version
        attributes 'Implementation-Vendor': 'time4tea technology'
        attributes 'Implementation-Vendor-Id': 'net.time4tea'
        attributes 'Built-On': ZonedDateTime.now()
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName

            pom.withXml {
                asNode().appendNode('name', archivesBaseName)
                asNode().appendNode('description', description)
                asNode().appendNode('url', 'https://github.com/time4tea-net/tea-time')
                asNode().appendNode('developers').appendNode('developer').
                        appendNode('name', 'James Richardson').parent().
                        appendNode('email', 'james+tea@time4tea.net')
                asNode().appendNode('scm').
                        appendNode('url', 'https://github.com/time4tea-net/tea-time.git').parent().
                        appendNode('connection', 'scm:git:https://github.com/time4tea-net/tea-time.git').parent().
                        appendNode('developerConnection', 'scm:git:git@github.com:time4tea-net/tea-time.git')
                asNode().appendNode('licenses').appendNode('license').
                        appendNode('name', 'Apache License, Version 2.0').parent().
                        appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.html')
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.bintrayUser : 'BINTRAYUSER'
    key = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : 'BINTRAYKEY'

    publications = ['mavenJava']
    configurations = [ 'archives' ]
    publish = true
    dryRun = false
    pkg {
        repo = 'oss'
        name = archivesBaseName

        desc = description
        websiteUrl = 'https://github.com/time4tea-net/tea-time'
        issueTrackerUrl = 'https://github.com/time4tea-net/tea-time/issues'
        vcsUrl = 'https://github.com/time4tea-net/tea-time.git'
        licenses = ['Apache-2.0']
        labels = []
        publicDownloadNumbers = true
        publish = true
        version {
            name = project.version
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

