import java.time.ZonedDateTime

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

group = "net.time4tea"
version = project.hasProperty("VERSION") ? project.VERSION : '0.0.1-SNAPSHOT'

description = "A small library to help you control time in your application, so you can test it"

sourceCompatibility = 1.8

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes 'Implementation-Title': 'tea-time'
        attributes 'Implementation-Version': project.version
        attributes 'Implementation-Vendor': 'time4tea technology'
        attributes 'Implementation-Vendor-Id': 'net.time4tea'
        attributes 'Built-On': ZonedDateTime.now()
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}


task javadocJar(type: Jar) {
    from javadoc
}

artifacts {
    archives javadocJar, sourceJar
}

signing {
    sign configurations.archives
}

nexusStaging {
    username = ossrhUser
    password = ossrhPassword
}

uploadArchives {
    repositories {
        mavenDeployer {
            // POM signature
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            // Target repository
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUser, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUser, password: ossrhPassword)
            }
            pom.project {
                name project.name
                description project.description
                packaging 'jar'
                url 'https://github.com/time4tea-net/tea-time'

                scm {
                    connection 'scm:git:https://github.com/time4tea-net/tea-time.git'
                    developerConnection 'scm:git:git@github.com:time4tea-net/tea-time.git'
                    url 'https://github.com/time4tea-net/tea-time.git'
                }

                licenses {
                    license {
                        name "Apache 2.0"
                        url 'https://opensource.org/licenses/Apache-2.0'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'time4tea'
                        name = 'James Richardson'
                        email = 'james+teatime@timetea.net'
                    }
                }
            }
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}
